# Maintainer: FadeMind <fademind@gmail.com>

pkgname=copyq
pkgver=3.3.1
pkgrel=1
pkgdesc="Clipboard manager with searchable and editable history."
url="https://github.com/hluk/CopyQ"
arch=('x86_64')
license=('GPL3')
depends=('libxtst' 'qt5-script' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('cmake' 'qt5-tools' 'qt5-svg')
replaces=('copyq-git' 'copyq-plugin-itemweb-git' 'copyq-plugin-itemweb')
conflicts=('copyq-git' 'copyq-plugin-itemweb-git' 'copyq-plugin-itemweb')
source=("${url}/archive/v${pkgver}.tar.gz")
sha256sums=('504ca31e8da47c67463d779348c46ff97369138694bc4fbe5adf08b9b38b68bd')

prepare() {
	mv CopyQ-${pkgver} ${pkgname}
	cd ${pkgname}
    if [[ -d build ]]; then
        rm -Rf build
    fi
    mkdir -p build
}

build() {
    cd ${pkgname}/build
    cmake \
    	-DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_QT5=TRUE \
        ${srcdir}/${pkgname}
    make
}

package() {
    cd ${pkgname}/build
    make DESTDIR="${pkgdir}" install
}

