# Maintainer: FadeMind <fademind@gmail.com>

pkgname=copyq
pkgver=3.3.0
pkgrel=4
_commit=37e664ca595f5bf4328b72de793960bc00a6ec0a
pkgdesc="Clipboard manager with searchable and editable history."
url="https://github.com/hluk/CopyQ"
arch=('x86_64')
license=('GPL3')
depends=('libxtst' 'qt5-script' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('cmake' 'qt5-tools' 'qt5-svg')
replaces=('copyq-git' 'copyq-plugin-itemweb-git' 'copyq-plugin-itemweb')
conflicts=('copyq-git' 'copyq-plugin-itemweb-git' 'copyq-plugin-itemweb')
source=("$url/archive/$_commit.tar.gz")
sha256sums=('173afa3dfe13e930d3258ff641ea092d761adfa3baca47a349e6a3adad45f465')

prepare() {
	mv CopyQ-$_commit $pkgname
	cd ${pkgname}
    if [[ -d build ]]; then
        rm -Rf build
    fi
    mkdir -p build
}

build() {
    cd ${pkgname}/build
    cmake \
    	-DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_QT5=TRUE \
        ${srcdir}/${pkgname}
    make
}

package() {
    cd ${pkgname}/build
    make DESTDIR="${pkgdir}" install
}

