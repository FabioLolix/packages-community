# $Id: PKGBUILD 123401 2014-12-03 12:28:05Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Graeme Gott <graeme@gottcode.org>

pkgname=xfce4-whiskermenu-plugin-gtk3
_pkgname=xfce4-whiskermenu-plugin
pkgver=2.1.6
pkgrel=1
pkgdesc='Alternate menu for Xfce4 (GTK3)'
arch=('x86_64' 'i686')
url='http://gottcode.org/xfce4-whiskermenu-plugin/'
groups=('xfce4-gtk3-goodies')
license=('GPL2')
depends=('xfce4-panel-gtk3' 'exo-gtk3' 'libxfce4ui-gtk3' 'garcon-gtk3')
makedepends=('cmake' 'ninja')
provides=($_pkgname)
conflicts=($_pkgname)
#source=("http://gottcode.org/$pkgname/$pkgname-$pkgver.tar.bz2")
source=("http://mirror.netcologne.de/xfce/src/panel-plugins/xfce4-whiskermenu-plugin/${pkgver%.*}/xfce4-whiskermenu-plugin-$pkgver.tar.bz2")
sha256sums=('8b2a8ee1445df39a2cda139e353f2e9ec3720a780296dc41b7d4aebde57371fc')

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/$_pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim:set ts=2 sw=2 et:
