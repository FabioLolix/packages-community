# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Contributor: Vitor Lopes <vmnlop@gmail.com>

pkgname=('ms-office-online')
pkgver=18.04.1
pkgrel=4
pkgdesc="Microsoft Office Suite Online for Manjaro Linux"
url="https://www.office.com/"
depends=('python-jade-application-kit>=0.36')
license=('GPL3' 'custom')
arch=('any')
provides=('microsoft-office-online' 'microsoft-office')
source=("${pkgname}-${pkgver}-${pkgrel}.tar.gz::https://github.com/manjaro/${pkgname}-launcher/archive/${pkgver}.tar.gz"
        "${pkgname}-${pkgver}-${pkgrel}.patch::https://github.com/manjaro/ms-office-online-launcher/compare/${pkgver}...c1acc54.patch")
sha256sums=('f7bc899379fa4999ed2b2d74b0a6db375018f42c625cb557b9885a20a5f61e8a'
            'e236e3b504d006fe493c5945d1590fa8cb271e017d526f2fc8639aceff181320')

prepare() {
    cd "${pkgname}-launcher-${pkgver}"
    patch -p1 -i "${srcdir}/${pkgname}-${pkgver}-${pkgrel}.patch"
}

package() {
    cd "${pkgname}-launcher-${pkgver}"
    make PREFIX="/usr" DESTDIR="${pkgdir}" install

    # Workaround till issue #2 is fixed
    rm "${pkgdir}"/usr/bin/*

    # Create binaries
    declare -a arr=(ms-office ms-powerpoint ms-excel ms-word ms-outlook ms-onenote ms-skype)
    for i in ${arr[@]}; do
        echo "#!/bin/sh" > "${pkgdir}/usr/bin/$i"
        echo "cd /usr/share/ms-office/${i//ms-/}" >> "${pkgdir}/usr/bin/$i"
        echo "./ms-office-online" >> "${pkgdir}/usr/bin/$i"
    done
    chmod a+x "${pkgdir}"/usr/bin/*

    # Workaround till issue #3 is fixed
    for i in $(ls "${pkgdir}"/usr/bin); do
        sed -i -e "s|^Icon=.*|Icon=/usr/share/icons/hicolor/1024x1024/apps/$i.png|g" \
        "${pkgdir}/usr/share/applications/$i.desktop"
    done
}
