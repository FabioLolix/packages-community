# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_pkgname=deepin-desktop-schemas
pkgname=$_pkgname-manjaro
pkgver=3.2.12
pkgrel=1
pkgdesc='Manjaro Linux Deepin gschemas'
arch=('any')
url="https://github.com/linuxdeepin/$_pkgname"
license=('GPL3')
groups=('deepin-manjaro')
depends=('dconf'
    'deepin-iconthemes-manjaro'
    'deepin-sound-theme'
    'deepin-wallpapers'
    'noto-fonts'
    'noto-fonts-compat')
makedepends=('git')
conflicts=("$_pkgname"
    'deepin-artwork-themes')
replaces=('deepin-artwork-themes' 'deepin-artwork-themes-manjaro')
provides=("$_pkgname")
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-desktop-schemas/archive/$pkgver.tar.gz"
    'manjaro.diff')
sha256sums=('6a7b3edce2f45e96e9d29da4e9d071282a08e4174306b00b9906f154d163b80c'
            '3d2d1e29bb41d940c425b47726515f4b480d12b65e130f274760fe1c2a2231da')

prepare() {
    cd $_pkgname-$pkgver
    patch -p1 < ../manjaro.diff
}

build() {
    cd $_pkgname-$pkgver
    make ARCH=x86
}

package() {
    cd $_pkgname-$pkgver
    make DESTDIR=$pkgdir install
}
