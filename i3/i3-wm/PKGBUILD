# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>

pkgname=i3-wm
pkgver=4.15
pkgrel=1.1
pkgdesc="An improved dynamic tiling window manager"
url="https://i3wm.org/"
arch=(x86_64)
license=(BSD)
depends=(xcb-util-cursor xcb-util-keysyms xcb-util-wm xcb-util-xrm libev yajl startup-notification
         pango libxkbcommon-x11)
makedepends=(bison flex asciidoc xmlto)
optdepends=('dmenu: As menu.'
            'i3lock: For locking your screen.'
            'i3status: To display systeminformation with a bar.'
            'perl: i3-save-tree and i3-dmenu-desktop'
            'perl-anyevent-i3: Features like saving the layout.'
            'perl-json-xs: Features like saving the layout.')
backup=(etc/i3/config)
groups=(i3)
replaces=(i3 i3bar)
source=("https://i3wm.org/downloads/i3-$pkgver.tar.bz2"{,.asc}
        '0001-Use-OVER-operator-for-drawing-text.patch'
        'reframe_swallowed_windows.patch::https://github.com/i3/i3/pull/3301/commits/7ac37d8ae463d749c9f5aeb9428ed3523a5a53fb.patch')
sha256sums=('217d524d1fbc85ae346b25f6848d1b7bcd2c23184ec88d29114bf5a621385326'
            'SKIP'
            '0fb308a0a3e0d592567fd81d3856277dd0670dd0545dadf1e2f3d01e8a335763'
            'a57aab196c422b5a4f98f087adf4c911956d8706cfe90431f0457a145d32d358')
validpgpkeys=('424E14D703E7C6D43D9D6F364E7160ED4AC8EE1D') # Michael Stapelberg

prepare() {
  mkdir build
  cd i3-$pkgver

  # https://github.com/i3/i3/pull/2925
  patch -Np1 -i ../0001-Use-OVER-operator-for-drawing-text.patch
  patch -Np1 -i ../reframe_swallowed_windows.patch

  autoreconf -fvi
}

build() {
  cd build
  ../i3-$pkgver/configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/man/man1" -m644 man/*.1
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 ../i3-$pkgver/LICENSE
}
