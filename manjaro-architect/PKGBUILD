# Maintainer: Chrysostomus @forum.manjaro.org
# Maintainer: Bernhard Landauer <oberon@manjaro.org>

pkgbase=manjaro-architect
pkgname=('manjaro-architect' 'manjaro-architect-launcher')
pkgver=0.9.10
pkgrel=1
pkgdesc="Manjaro CLI net-installer, forked from the Archlinux Architect"
arch=(any)
url="https://github.com/Chrysostomus/$pkgbase"
license=(GPL2)
depends=('bash'
         'fzf'
         'dialog')
optdepends=('maia-console'
            'terminus-font')
source=("$url/archive/$pkgver.tar.gz")
md5sums=('d6a2149929cf1d37ae8a7b65d964f033')

prepare() {
    cd $pkgbase-$pkgver
    sed -i "s/Version .*/Version = $pkgver/" Makefile
}

build() {
    cd $pkgbase-$pkgver
	make PREFIX=/usr
}

package_manjaro-architect() {
    depends+=('f2fs-tools'
         'gptfdisk'
         'manjaro-architect-launcher'
         'manjaro-tools-base'
         'mhwd'
         'nilfs-utils'
         'pacman-mirrorlist'
         'parted')
    cd $pkgbase-$pkgver
	make PREFIX=/usr DESTDIR=${pkgdir} install
}

package_manjaro-architect-launcher() {
    cd $pkgbase-$pkgver
    install -Dm755 bin/setup $pkgdir/usr/bin/setup
}
