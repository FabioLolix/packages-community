# Maintainer: Chrysostomus @forum.manjaro.org
# Maintainer: Bernhard Landauer <oberon@manjaro.org>

pkgbase=manjaro-architect
pkgname=('manjaro-architect' 'manjaro-architect-launcher')
pkgver=0.9.11
_commit=7d7f7595d347e1c1fd73b46669904c4c300238eb
pkgrel=1
pkgdesc="Manjaro CLI net-installer, forked from the Archlinux Architect"
arch=(any)
url="https://github.com/Chrysostomus/$pkgbase"
license=(GPL2)
depends=('bash'
         'fzf'
         'dialog')
optdepends=('maia-console'
            'terminus-font')
source=("$url/archive/$pkgver.tar.gz")
md5sums=('1965db01263258353919accd901b3117')

prepare() {
    cd $pkgbase-$pkgver
    sed -i "s/Version .*/Version = $pkgver/" Makefile
}

build() {
    cd $pkgbase-$pkgver
	make PREFIX=/usr
}

package_manjaro-architect() {
    depends+=('f2fs-tools'
         'gptfdisk'
         'manjaro-architect-launcher'
         'manjaro-tools-base'
         'mhwd'
         'nilfs-utils'
         'pacman-mirrorlist'
         'parted')
    cd $pkgbase-$pkgver
	make PREFIX=/usr DESTDIR=${pkgdir} install
}

package_manjaro-architect-launcher() {
    cd $pkgbase-$pkgver
    install -Dm755 bin/setup $pkgdir/usr/bin/setup
}
