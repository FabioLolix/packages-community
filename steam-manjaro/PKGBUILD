# Maintainer: Fabian Bornschein <plusfabi(At)gmail(Dot)com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>
# Contributor: Alexandru Ianu <alexandru@manjaro.org>
# Contributor: Manuel HÃ¼sers <manuel.huesers@uni-ol.de>

pkgname=steam-manjaro
_package=steam
pkgver=1.0.0.54
pkgrel=12
pkgdesc="Valve's digital software delivery system"
url='http://steampowered.com/'
arch=('i686' 'x86_64')
license=('custom')
depends=('lsb-release' 'desktop-file-utils' 'curl' 'dbus' 'freetype2' 'gdk-pixbuf2' 'ttf-font' 'xorg-xrandr' 'zenity' 'steam-devices')
depends_x86_64=('lib32-libgl' 'lib32-mesa' 'lib32-gcc-libs' 'lib32-libx11' 'lib32-libxss' 'lib32-alsa-plugins')
optdepends=('steam-native: steam native runtime support')
provides=('steam')
conflicts=('steam')
source=(http://repo.steampowered.com/${_package}/pool/${_package}/s/${_package}/${_package}_${pkgver}.tar.gz
        '0001-alsa-sdl-audiodriver.patch'
        '0002-dbus_fatal_warning.patch'
        '0003-manjaro-steam-native.patch'
        '0004-steam_frame_force_close.patch'
        'steam_install_workaround'
        )
sha512sums=('1820f596359d829a4e119f1f916a741ecb7973d36684916cdbbfa435fcdc9c288852491fd6d0b064e8154a43c60d8dcce3c7ed9275aed8f5f116c2782489bf32'
            'ec57f189fbc25633799c553b0eb30bd074f02befd9c7e7ca1c052be0a66ca3745b511b0fccb21ddb57885d60a23c8d40eaa8d63f8da2fc2fff3b8e7f5d23b091'
            '12d8b5786e2a375ec03131ff5c246c64793acc24cf42e74b40cd77a031c17543da05f7dd33b1f17f63524710f5d96636f35908e1953292f44e949f84fb753c27'
            'fec7a63231c502debaa8ccdac73270e47ad42dd85e420eee44100ed0e4e4678e9ad70e36a3df02cc9ebe9936b710737d0867532a9b319bc1bee141c420db5900'
            '62a9d7f216a71588562618129e75cbd5247477ad28b9c4a81c6dd5ed5bb2c35a379c18b82fe10b18ffbd94d4c1bba1ea11f40f2152c75694c5f68a544caaad17'
            'a5eb16810f2e1f1b6fbb627dba7ae74da2f375885f79ab829f918f541861ccde360c6d658b30236e7b8fad789ab98a1c35be1a08f6b19853ec338863b8caccc8')

prepare()
{
    cd ${_package}
    patch -Np1 < "${srcdir}/0001-alsa-sdl-audiodriver.patch"
    patch -Np1 < "${srcdir}/0002-dbus_fatal_warning.patch"
    patch -Np1 < "${srcdir}/0003-manjaro-steam-native.patch"
    patch -Np1 < "${srcdir}/0004-steam_frame_force_close.patch"
}

package()
{
    make -C "${_package}" DESTDIR="${pkgdir}" install

    # install license
    install -dm 755 "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    ln -s "/usr/share/doc/steam/steam_install_agreement.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    #install changelog
    install -Dm 644 "${_package}/debian/changelog" -t "${pkgdir}/usr/share/doc/${_package}"

    # blank steamdeps because apt-get
    ln -sf /usr/bin/true "${pkgdir}/usr/bin/steamdeps"

    # bug #4816 https://github.com/ValveSoftware/steam-for-linux/issues/4816
    install -D -m755 "${srcdir}/steam_install_workaround" \
    "${pkgdir}/usr/bin/steam_install_workaround"
}
