# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>
# Maintainer: Sam Burgos < sam dot burgos1089 at gmail dot com >

pkgname='lightdm-slick-greeter'
_pkgname='slick-greeter'
pkgver=1.1.4
pkgrel=2
pkgdesc='A slick-looking LightDM greeter'
arch=(i686 x86_64)
url="https://github.com/linuxmint/${_pkgname}"
license=('GPL3')
source=("${url}/archive/${pkgver}.tar.gz"
        "$_pkgname.conf"
        "$_pkgname.png")
depends=('cairo'
    'freetype2'
    'gtk3'
    'libcanberra'
    'libxext'
    'lightdm'
    'manjaro-wallpapers-17.0'
    'pixman')
makedepends=('intltool'
    'gnome-common'
    'vala')
backup=('etc/lightdm/slick-greeter.conf')
sha256sums=('a73e5f8a448731b6497fbf6a6599a38892ea316f6c075e3b63a14911ffdf565d'
            '5cb1f87954c0f0749296d6431347b2bf4ae0a5d8cc4ede481dd40d257d7e67c2'
            '4009e8dcee20a46273f4af28604618f4210c366d0be2fc647c4eed58fb590493')

build() {
    cd ${_pkgname}-${pkgver}
    aclocal --install
    autoreconf -vfi
    intltoolize -f
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --libexecdir=/usr/lib/lightdm
    make
}

package() {
	cd ${_pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    # adjust launcher name
    mv $pkgdir/usr/share/xgreeters/slick-greeter.desktop \
      $pkgdir/usr/share/xgreeters/lightdm-slick-greeter.desktop
    # Install default conf 
    install -Dm644 $srcdir/$_pkgname.conf $pkgdir/etc/lightdm/$_pkgname.conf
    install -Dm644 $srcdir/$_pkgname.png $pkgdir/usr/share/backgrounds/$_pkgname.png
}
