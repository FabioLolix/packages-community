# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# AUR-Maintainer: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xviewer-plugins
_gitrel=master.mint19
pkgver=master.lmde3.r0.gba9ce0f
pkgrel=1
_commit=ba9ce0f2d0bb923a56a61a6b0c03de6d15440d50 #tag master.mint19
pkgdesc="Plugins for xviewer. X-Apps Project"
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/linuxmint/$pkgname"
groups=('x-apps')
depends=('libpeas' 'xviewer')
makedepends=('git' 'gnome-common')
source=("git+$url.git#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd $pkgname
    ./autogen.sh --prefix="/usr" \
        --localstatedir="/var" \
        --libexecdir="/usr/lib/${pkgname}"
    make
}

package(){
    cd $pkgname
    make DESTDIR="$pkgdir/" install
}
