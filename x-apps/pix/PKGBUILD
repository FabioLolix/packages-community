# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# AUR-Maintainer: Nate Simon <aurpkg (at natesimon.net)>

pkgname=pix
_gitrel=master.mint19
pkgver=1.6.2+$_gitrel
pkgrel=1
pkgdesc="Image viewer and browser based on gthumb. X-Apps Project"
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/linuxmint/$pkgname"
groups=('x-apps')
depends=('clutter-gtk'
    'dcraw'
    'desktop-file-utils'
    'gconf'
    'gsettings-desktop-schemas'
    'gst-plugins-base-libs'
    'librsvg'
    'libwebp'
    'webkit2gtk')
makedepends=('exiv2'
    'gnome-common'
    'intltool'
    'itstool'
    'libchamplain'
    'liboauth'
    'libopenraw')
optdepends=('brasero: Burn discs'
    'exiv2: Embedded metadata support'
    'gstreamer: Video support'
    'libchamplain: Map Viewer'
    'libjpeg-turbo: Jpeg writing support'
    'liboauth: Web albums'
    'libopenraw: Support for RAW photos'
    'libtiff: Tiff writing support'    )
source=("$url/archive/$_gitrel.tar.gz")
md5sums=('7e09e5fc1831a895eb40de815bdd9534')

build() {
    cd $pkgname-$_gitrel
    gnome-autogen.sh --prefix="/usr" \
        --localstatedir="/var" \
        --libexecdir="/usr/lib/pix" \
        --disable-static

    # Copy some files that ended up in the wrong directory.
    # This doesn't happen on mint. See this github issue:
    # https://github.com/linuxmint/pix/issues/7
    if [ -d 'tests/$(top_srcdir)/' ]
    then
        cp 'tests/$(top_srcdir)/pix/.deps/dom_test-dom.Po' "pix/.deps/"
        cp 'tests/$(top_srcdir)/pix/.deps/glib_utils_test-glib-utils.Po' "pix/.deps/"
        cp 'tests/$(top_srcdir)/pix/.deps/gsignature_test-gsignature.Po' "pix/.deps/"
        cp 'tests/$(top_srcdir)/pix/.deps/oauth_test-gsignature.Po' "pix/.deps/"
    fi

    make
}

package(){
    cd $pkgname-$_gitrel
    make DESTDIR="$pkgdir/" install
}
