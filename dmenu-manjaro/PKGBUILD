# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Chrysostomus @forum.manjaro.org
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Jonathon Fernyhough <jonathon at manjaro_dot_org>

pkgname=dmenu-manjaro
_pkgname=dmenu
pkgver=4.8
pkgrel=1
pkgdesc="customized recency-aware dmenu for manjaro-i3 with xft- and mouse-support"
arch=('i686' 'x86_64')
groups=('i3-manjaro')
license=('MIT')
depends=('freetype2'
         'libxft'
         'libxinerama'
         'noto-fonts'
         'sh')
conflicts=("$_pkgname" "$_pkgname-xft")
provides=("$_pkgname" "$_pkgname-xft")
install=$pkgname.install
source=("https://dl.suckless.org/tools/$_pkgname-$pkgver.tar.gz"
        '001-dmenu-4.8-mousesupport-manjaro.diff'
        '002-dmenu-4.8-line-height-manjaro.diff'
        '003-dmenu-4.8-xyw-manjaro.diff'
        'dmenu_recency'
        'dmenurc')
sha256sums=('fe615a5c3607061e2106700862e82ac62a9fa1e6a7ac3d616a9c76106476db61'
            '2f4038ab07432dec57556d847c3e0104528234fe07f0ada21972d8f610408d95'
            '2eb6f4f206017e26b21b909be8070e49f44613a8178c5c21314cc15fcf03be31'
            '63cb0925f0984c62fa4e0943f5d1463c740a14e58a7596cc326e8c51b4b6592b'
            'cfef201547d14bfdb8f192dce369502f015c860e1d84b54fe960f3b9ffbc059c'
            'c0b773af5a1458036d242b19f2e45ed444e6e86d2cf15bedbfb996e8c2fd05b6')

prepare() {
	cd $_pkgname-$pkgver
	msg "mouse"
	patch -p1 < ../001-dmenu-4.8-mousesupport-manjaro.diff
	msg "line-height"
	patch -p1 < ../002-dmenu-4.8-line-height-manjaro.diff
	msg "xyw"
	patch -p1 < ../003-dmenu-4.8-xyw-manjaro.diff
}
build() {
	cd $_pkgname-$pkgver
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
	cd $_pkgname-$pkgver
	make PREFIX=/usr DESTDIR="$pkgdir" install
	install -Dm755 "$srcdir/dmenu_recency" "$pkgdir/usr/bin/dmenu_recency"
	install -Dm755 "$srcdir/dmenurc" "$pkgdir/usr/share/$_pkgname/dmenurc"
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
